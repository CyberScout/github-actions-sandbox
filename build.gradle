plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'com.cyberscout.sandbox'
version '1.2.2'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

//
// Maven Publishing
//
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId = project.group
            artifactId = project.name
            version = project.version
            pom {
                name = 'GitHub Actions Sandbox'
                description = 'Testbed for GitHub Actions'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'cyberscout'
                        name = 'CyberScout, LLC'
                        email = 'dev-support@cyberscout.com'
                        url = 'https://cyberscout.com'
                    }
                }
                scm {
                    connection = 'https://github.com/CyberScout/github-actions-sandbox.git'
                    developerConnection = 'git@github.com:CyberScout/github-actions-sandbox.git'
                    url = 'https://github.com/CyberScout/github-actions-sandbox'
                }
            }
        }
    }
}

//
// Bintray Publishing
//
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    publish = true
    override = true
    pkg {
        userOrg = 'cyberscout'
        repo = 'cyberscout-oss-maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/CyberScout/github-actions-sandbox.git'
        githubRepo = 'CyberScout/github-actions-sandbox'
        publicDownloadNumbers = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

//
// Uber-Publish Task
//
task cdPublish(dependsOn: [check, bintrayUpload]) {
    description 'Publish artifacts to distribution points'
    group 'publishing'
}

bintrayUpload.mustRunAfter check
